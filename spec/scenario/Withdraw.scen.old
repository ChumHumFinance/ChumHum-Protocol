-- Withdraw Tests

Test "Supply MATIC 5 then Withdraw MAX in the same block"
	AddToken MATIC -- baseline sanity check for withdraw max
	SupportMarket MATIC (FixedPrice 1.0) (FixedRate 0.5 0.75) SimplePolicyHook
	Approve Geoff MATIC "6.0e18"
	Faucet Geoff MATIC "6.0e18"
	Supply Geoff MATIC "5.0e18"
	Assert Success
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "5.0e18")
	Assert Equal (BorrowBalance Geoff MATIC) (Exactly "0e18")
	Assert Equal (MaxBorrow Geoff) (Exactly "2.5e18")
	Withdraw Geoff MATIC "MAX"
	Assert Success
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "0.0e18")
	Assert Equal (TokenBalance Geoff MATIC) (Exactly "6e18")

Test "Supply MATIC 5 then Withdraw MAX (6) after accruing some interest"
	AddToken MATIC
	SupportMarket MATIC (FixedPrice 1.0) (FixedRate 0.5 0.75) SimplePolicyHook
	Approve Geoff MATIC "6.0e18"
	Faucet Geoff MATIC "6.0e18"
	Supply Geoff MATIC "5.0e18" -- We need more MATIC in the system to simulate protocol gaining borrow interest to pay Geoff
	Approve Torrey MATIC "10.0e18"
	Faucet Torrey MATIC "10.0e18"
	Supply Torrey MATIC "10.0e18"
	Assert Success
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "5.0e18")
	FastForward 2 Blocks
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "10.0e18")
	Withdraw Geoff MATIC "MAX"
	Assert Success
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "0.0e18")
	Assert Equal (TokenBalance Geoff MATIC) (Exactly "11e18")

Test "Withdraw MATIC 1 when contract paused"
	AddToken MATIC
	SupportMarket MATIC (FixedPrice 1.0) (FixedRate 0.5 0.75) SimplePolicyHook
	Approve Geoff MATIC "1.0e18"
	Faucet Geoff MATIC "1.0e18"
	Supply Geoff MATIC "1.0e18"
	Assert Success
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "1.0e18")
	PolicyHook MATIC (SetProtocolPaused True)
	Withdraw Geoff MATIC "1.0e18"
	Assert Failure COMPTROLLER_REJECTION WITHDRAW_COMPTROLLER_REJECTION 1
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "1e18")
	Assert Equal (TokenBalance Geoff MATIC) (Exactly "0e18")
