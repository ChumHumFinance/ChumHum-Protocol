-- Supply Tests

Test "Geoff supplies MATIC and we check 2 future balances and then supply again"
	AddToken MATIC
	SupportMarket MATIC (FixedPrice 1.0) (FixedRate 0.5 0.75) SimplePolicyHook
	Approve Geoff MATIC "10.0e18"
	Faucet Geoff MATIC "10.0e18"
	Supply Geoff MATIC "3e18"
	Assert Success
	FastForward 2 Blocks
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "6.0e18") -- 3 * ( 1 + 2 * .5 )
	FastForward 2 Blocks
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "9.0e18") -- 3 * ( 1 + 4 * .5 )
	Supply Geoff MATIC "1e18"
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "10.0e18") -- 3 * ( 1 + 4 * .5 ) + 1
	FastForward 2 Blocks
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "20.0e18") -- 10 * ( 1 + 2 * .5 )

Test "Geoff supplies MATIC, Torrey supplies MATIC and then Geoff supplies more MATIC"
	AddToken MATIC
	SupportMarket MATIC (FixedPrice 1.0) (FixedRate 0.5 0.75) SimplePolicyHook
	Approve Geoff MATIC "10.0e18"
	Faucet Geoff MATIC "10.0e18"
	Approve Torrey MATIC "5.0e18"
	Faucet Torrey MATIC "5.0e18"
	Supply Geoff MATIC "1e18"
	Assert Success
	FastForward 2 Blocks
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "2.0e18")
	Supply Torrey MATIC "3e18"
	Assert Success
	FastForward 2 Blocks
	Assert Equal (SupplyBalance Torrey MATIC) (Exactly "6.0e18")
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "4.0e18")
	Supply Geoff MATIC "1e18"
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "5.0e18")

Test "Can't supply an 'initial' asset"
	AddToken Dragon
	Approve Geoff Dragon "10.0e18"
	Faucet Geoff Dragon "10.0e18"
	Supply Geoff Dragon "1e18"
	Assert Failure MARKET_NOT_LISTED SUPPLY_MARKET_NOT_LISTED

Test "Can't supply when contract is paused"
	AddToken MATIC
	SupportMarket MATIC (FixedPrice 1.0) (FixedRate 0.5 0.75) SimplePolicyHook
	Approve Geoff MATIC 1.0e18
	Faucet Geoff MATIC 0.4e18
	PolicyHook MATIC (SetProtocolPaused True)
	Supply Geoff MATIC 0.3e18
	Assert Failure COMPTROLLER_REJECTION SUPPLY_COMPTROLLER_REJECTION 1
	Assert Equal (SupplyBalance Geoff MATIC) (Exactly "0e18")

Test "With always paused policy hook, can't supply when contract is paused"
	AddToken MATIC
	SupportMarket MATIC (FixedPrice 1.0) (FixedRate 0.5 0.75) AlwaysPausedPolicyHook
	Supply Geoff MATIC 0.3e18
	Assert Failure COMPTROLLER_REJECTION SUPPLY_COMPTROLLER_REJECTION 99
