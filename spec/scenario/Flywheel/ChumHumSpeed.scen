-- Tests for the grants and math patch

Macro FlywheelComptroller price=1.0 borrowRate=0.000005 chumhumInitAmount=5000000e18
    Unitroller Deploy
    PriceOracle Deploy Fixed price
    PriceOracleProxy Deploy Admin (PriceOracle Address) (Address Zero) (Address Zero) (Address Zero) (Address Zero) (Address Zero)
    ----g1
    ComptrollerImpl Deploy ScenarioG1 ComptrollerScenG1
    Unitroller SetPendingImpl ComptrollerScenG1
    ComptrollerImpl ComptrollerScenG1 BecomeG1
    --list some tokens
    Comptroller SetPriceOracle (PriceOracleProxy Address)
    Comptroller SetMaxAssets 20
    Comptroller SetCloseFactor 0.5
    Comptroller LiquidationIncentive 1.1
    Comptroller SetChumHumRate 1e18
    NewCToken ZRX cZRX
    NewCToken BAT cBAT
    Support cZRX collateralFactor:0.5
    Support cBAT collateralFactor:0.5
    Comptroller AddChumHumMarkets (cZRX cBAT)
    Erc20 Deploy Standard CHUM "CHUM Token" 18
    Give (Address Comptroller) chumhumInitAmount CHUM
    Comptroller Send "setCHUMAddress(address)" (Address CHUM)
    Erc20 Deploy Standard BUM "BUM Token" 18
    Give (Address Comptroller) chumhumInitAmount BUM
    Comptroller Send "setBUMAddress(address)" (Address BUM)

Macro GrantsComptroller
    FlywheelComptroller
    -- g2
    ComptrollerImpl Deploy ScenarioG2 ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen BecomeG2
    -- current
    ComptrollerImpl Deploy Scenario ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen Become

Macro InitSpeeds
    Prep Geoff 100e18 ZRX cZRX
    Mint Geoff 50e18 cZRX--tokenbalance = 50e18 / 2e9 = 2.5e10
    Prep Coburn Some BAT cBAT
    Mint Coburn 6e18 cBAT--tokenbalance = 6e18 / 2e9 = 3e9
    EnterMarkets Coburn cBAT
    Borrow Coburn 1e18 cZRX
    Comptroller SetChumHumSpeed cZRX 1
    Comptroller SetChumHumSpeed cBAT 1
    Comptroller RefreshChumHumSpeeds
    Comptroller Send "setCHUMAddress(address)" (Address CHUM)

Test "CHUM speed can be set per market"
    GrantsComptroller
    InitSpeeds
    -- Baseline chum amounts
    Assert Equal (Comptroller ChumHumAccrued Geoff) 0
    Assert Equal (Erc20 CHUM TokenBalance Geoff) 0
    -- ChumHum speed can be set
    Comptroller SetChumHumSpeed cZRX 2
    FastForward 1000 Blocks
    Comptroller ClaimChumHum Geoff
    Assert Equal (Comptroller ChumHumAccrued Geoff) 0
    Assert Equal (Erc20 CHUM TokenBalance Geoff) 2000
    -- ChumHum speed can be changed
    Comptroller SetChumHumSpeed cZRX 4
    FastForward 1000 Blocks
    Comptroller ClaimChumHum Geoff
    Assert Equal (Comptroller ChumHumAccrued Geoff) 0
    Assert Equal (Erc20 CHUM TokenBalance Geoff) 6000
    -- ChumHum speed can be removed
    Comptroller SetChumHumSpeed cZRX 0
    FastForward 1000 Blocks
    Comptroller ClaimChumHum Geoff
    Assert Equal (Comptroller ChumHumAccrued Geoff) 0
    Assert Equal (Erc20 CHUM TokenBalance Geoff) 6000

Test "Set chum rate is removed"
    GrantsComptroller
    InitSpeeds
    AllowFailures
    Comptroller SetChumHumRate 5

Test "CHUM is not claimed automatically"
    GrantsComptroller
    InitSpeeds
    Comptroller SetChumHumSpeed cZRX 2
    FastForward 100000 Blocks
    -- Check chum is not claimed automatically
    Mint Geoff 50e18 cZRX
    Assert Equal (Erc20 CHUM TokenBalance Geoff) 0
    -- Confirm there was chum to claim
    Comptroller ClaimChumHum Geoff
    Assert Equal (Erc20 CHUM TokenBalance Geoff) 200000
